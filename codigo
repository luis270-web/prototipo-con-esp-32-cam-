// Conexión del módulo OLED
//Pin GND-GND
//5V-VCC
//14-SCL
//15-SDA

#include <tarjetas_inferencing.h>     // Librería del modelo de Edge Impulse en nuestro caso se llama tarjetas 
#include "edge-impulse-sdk/dsp/image/image.hpp"
#include "esp_camera.h"               // Librería para la cámara ESP32-CAM

#define CAMERA_MODEL_AI_THINKER       // Modelo de cámara (versión AI Thinker)

// --- Configuración de pines del ESP32-CAM (modelo AI Thinker) ---
#if defined(CAMERA_MODEL_AI_THINKER)
#define PWDN_GPIO_NUM 32
#define RESET_GPIO_NUM -1
#define XCLK_GPIO_NUM 0
#define SIOD_GPIO_NUM 26
#define SIOC_GPIO_NUM 27

#define Y9_GPIO_NUM 35
#define Y8_GPIO_NUM 34
#define Y7_GPIO_NUM 39
#define Y6_GPIO_NUM 36
#define Y5_GPIO_NUM 21
#define Y4_GPIO_NUM 19
#define Y3_GPIO_NUM 18
#define Y2_GPIO_NUM 5
#define VSYNC_GPIO_NUM 25
#define HREF_GPIO_NUM 23
#define PCLK_GPIO_NUM 22
#else
#error "Camera model not selected"
#endif

// Tamaño original del frame de la cámara
#define EI_CAMERA_RAW_FRAME_BUFFER_COLS 320
#define EI_CAMERA_RAW_FRAME_BUFFER_ROWS 240
#define EI_CAMERA_FRAME_BYTE_SIZE 3     

// Librerías para el display OLED 
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

// Pines personalizados para la pantalla oled SDA Y SCL
#define I2C_SDA 15
#define I2C_SCL 14
TwoWire I2Cbus = TwoWire(0);

// Tamaño de pantalla OLED
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
#define SCREEN_ADDRESS 0x3C
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &I2Cbus, OLED_RESET);

// Pines de los LEDs indicador
#define LED_AZUL 12
#define LED_VERDE 13
#define LED_ROJO 2

static bool debug_nn = false;
static bool is_initialised = false;
uint8_t *snapshot_buf;   // lugar donde se guardará la imagen capturada

// Configuración de la cámara
static camera_config_t camera_config = {
  .pin_pwdn = PWDN_GPIO_NUM,
  .pin_reset = RESET_GPIO_NUM,
  .pin_xclk = XCLK_GPIO_NUM,
  .pin_sscb_sda = SIOD_GPIO_NUM,
  .pin_sscb_scl = SIOC_GPIO_NUM,

  // Pines de la señal de imagen
  .pin_d7 = Y9_GPIO_NUM,
  .pin_d6 = Y8_GPIO_NUM,
  .pin_d5 = Y7_GPIO_NUM,
  .pin_d4 = Y6_GPIO_NUM,
  .pin_d3 = Y5_GPIO_NUM,
  .pin_d2 = Y4_GPIO_NUM,
  .pin_d1 = Y3_GPIO_NUM,
  .pin_d0 = Y2_GPIO_NUM,

  .pin_vsync = VSYNC_GPIO_NUM,
  .pin_href = HREF_GPIO_NUM,
  .pin_pclk = PCLK_GPIO_NUM,

  .xclk_freq_hz = 20000000,       // Reloj de la cámara 
  .ledc_timer = LEDC_TIMER_0,
  .ledc_channel = LEDC_CHANNEL_0,

  .pixel_format = PIXFORMAT_JPEG, // Formato JPEG
  .frame_size = FRAMESIZE_QVGA,   // 320x240
  .jpeg_quality = 12,
  .fb_count = 1,
  .fb_location = CAMERA_FB_IN_PSRAM,
  .grab_mode = CAMERA_GRAB_WHEN_EMPTY,
};

// Declaración de funciones
bool ei_camera_init(void);
void ei_camera_deinit(void);
bool ei_camera_capture(uint32_t img_width, uint32_t img_height, uint8_t *out_buf);

void setup() {

  Serial.begin(115200);

  // Inicializar bus I2C para el OLED
  I2Cbus.begin(I2C_SDA, I2C_SCL, 100000);

  // Inicializar pantalla OLED
  if (!display.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS)) {
    Serial.printf("SSD1306 OLED display failed to initialize.\n");
    while (true);
  }

  // Configuración de LEDs
  pinMode(LED_AZUL, OUTPUT);
  pinMode(LED_VERDE, OUTPUT);
  pinMode(LED_ROJO, OUTPUT);

  // LED azul encendido = sistema listo pero sin detección
  digitalWrite(LED_AZUL, HIGH);
  digitalWrite(LED_VERDE, LOW);
  digitalWrite(LED_ROJO, LOW);

  Serial.println("Edge Impulse Inferencing Demo");

  // Inicializar cámara
  if (ei_camera_init() == false) {
    ei_printf("Failed to initialize Camera!\r\n");
  } else {
    ei_printf("Camera initialized\r\n");
  }

  // Mensaje de inicio en pantalla
  display.clearDisplay();
  display.setCursor(0, 0);
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);
  display.print("Starting continious\n inference in\n 2 seconds...");
  display.display();
  ei_sleep(2000);
  display.clearDisplay();
}

void loop() {

  display.clearDisplay();

  // Pequeña pausa
  if (ei_sleep(5) != EI_IMPULSE_OK) return;

  // Reservar memoria para la imagen capturada
  snapshot_buf = (uint8_t *)malloc(EI_CAMERA_RAW_FRAME_BUFFER_COLS *
                                   EI_CAMERA_RAW_FRAME_BUFFER_ROWS *
                                   EI_CAMERA_FRAME_BYTE_SIZE);

  if (snapshot_buf == nullptr) {
    ei_printf("ERR: Failed to allocate snapshot buffer!\n");
    return;
  }

  // Estructura requerida por Edge Impulse
  ei::signal_t signal;
  signal.total_length = EI_CLASSIFIER_INPUT_WIDTH * EI_CLASSIFIER_INPUT_HEIGHT;
  signal.get_data = &ei_camera_get_data;

  // Capturar imagen
  if (ei_camera_capture(EI_CLASSIFIER_INPUT_WIDTH,
                        EI_CLASSIFIER_INPUT_HEIGHT,
                        snapshot_buf) == false) {
    ei_printf("Failed to capture image\r\n");
    free(snapshot_buf);
    return;
  }

  ei_impulse_result_t result = { 0 };

  // Ejecutar el modelo
  EI_IMPULSE_ERROR err = run_classifier(&signal, &result, debug_nn);

  if (err != EI_IMPULSE_OK) {
    ei_printf("ERR: Failed to run classifier (%d)\n", err);
    return;
  }

  // Mostrar tiempos de procesamiento
  ei_printf("Predictions (DSP: %d ms., Classification: %d ms., Anomaly: %d ms.): \n",
            result.timing.dsp, result.timing.classification, result.timing.anomaly);

#if EI_CLASSIFIER_OBJECT_DETECTION == 1
  bool bb_found = result.bounding_boxes[0].value > 0;
  bool detecto_lufy = false;
  bool detecto_toga = false;

  // Recorrer todos los objetos detectados
  for (size_t ix = 0; ix < result.bounding_boxes_count; ix++) {
    auto bb = result.bounding_boxes[ix];
    if (bb.value == 0) continue;

    // Mostrar etiqueta y porcentaje
    display.setCursor(0, 20 * ix);
    display.setTextSize(2);
    display.setTextColor(SSD1306_WHITE);
    display.print(bb.label);
    display.print("-");
    display.print(int(bb.value * 100));
    display.print("%");
    display.display();

    // Bandera para LEDs
    if (strcmp(bb.label, "lufy") == 0) detecto_lufy = true;
    if (strcmp(bb.label, "toga") == 0) detecto_toga = true;
  }

  // Control de LEDs según el objeto detectado
  if (bb_found) {
    if (detecto_lufy) {
      digitalWrite(LED_AZUL, LOW);
      digitalWrite(LED_VERDE, HIGH);
      digitalWrite(LED_ROJO, LOW);
    }
    else if (detecto_toga) {
      digitalWrite(LED_AZUL, LOW);
      digitalWrite(LED_VERDE, LOW);
      digitalWrite(LED_ROJO, HIGH);
    }
    else {
      digitalWrite(LED_AZUL, HIGH); // Otro objeto
      digitalWrite(LED_VERDE, LOW);
      digitalWrite(LED_ROJO, LOW);
    }
  } else {
    // Nada detectado
    display.setCursor(0, 20);
    display.setTextSize(2);
    display.print("No detecto");
    display.display();

    digitalWrite(LED_AZUL, HIGH);
    digitalWrite(LED_VERDE, LOW);
    digitalWrite(LED_ROJO, LOW);
  }
#endif

  free(snapshot_buf);  // Liberar memoria
}

// --- Inicialización de la cámara ---
bool ei_camera_init(void) {
  if (is_initialised) return true;

  esp_err_t err = esp_camera_init(&camera_config);

  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x\n", err);
    return false;
  }

  // Configuración extra para el sensor OV3660
  sensor_t *s = esp_camera_sensor_get();
  if (s->id.PID == OV3660_PID) {
    s->set_vflip(s, 1);      // Voltear vertical
    s->set_brightness(s, 1); // Aumentar brillo
    s->set_saturation(s, 0);
  }

  is_initialised = true;
  return true;
}

// --- Desinicializar cámara ---
void ei_camera_deinit(void) {
  esp_err_t err = esp_camera_deinit();
  if (err != ESP_OK) {
    ei_printf("Camera deinit failed\n");
    return;
  }
  is_initialised = false;
}

// --- Captura de imagen ---
bool ei_camera_capture(uint32_t img_width, uint32_t img_height, uint8_t *out_buf) {

  if (!is_initialised) {
    ei_printf("ERR: Camera is not initialized\r\n");
    return false;
  }

  // Tomar foto
  camera_fb_t *fb = esp_camera_fb_get();
  if (!fb) {
    ei_printf("Camera capture failed\n");
    return false;
  }

  // Convertir JPEG a RGB888
  bool converted = fmt2rgb888(fb->buf, fb->len, PIXFORMAT_JPEG, snapshot_buf);
  esp_camera_fb_return(fb);

  if (!converted) {
    ei_printf("Conversion failed\n");
    return false;
  }

  // Cambiar tamaño si es necesario
  if ((img_width != EI_CAMERA_RAW_FRAME_BUFFER_COLS) ||
      (img_height != EI_CAMERA_RAW_FRAME_BUFFER_ROWS)) {

    ei::image::processing::crop_and_interpolate_rgb888(
      out_buf,
      EI_CAMERA_RAW_FRAME_BUFFER_COLS, EI_CAMERA_RAW_FRAME_BUFFER_ROWS,
      out_buf,
      img_width, img_height
    );
  }

  return true;
}

// --- Conversión de datos RGB a formato para Edge Impulse ---
static int ei_camera_get_data(size_t offset, size_t length, float *out_ptr) {
  size_t pixel_ix = offset * 3;
  size_t out_ptr_ix = 0;

  // Convertir cada píxel a un entero R+G+B
  while (length--) {
    out_ptr[out_ptr_ix++] = (snapshot_buf[pixel_ix + 2] << 16) +
                            (snapshot_buf[pixel_ix + 1] << 8) +
                            snapshot_buf[pixel_ix];
    pixel_ix += 3;
  }

  return 0;
}

// Validación del modelo
#if !defined(EI_CLASSIFIER_SENSOR) || EI_CLASSIFIER_SENSOR != EI_CLASSIFIER_SENSOR_CAMERA
#error "Invalid model for current sensor"
#endif
